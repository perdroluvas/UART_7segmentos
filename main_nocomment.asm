.include "m328Pdef.inc"

.equ VALOR_UBRR = 207

.cseg
.org 0x0000
	RJMP INICIO
.org INT0addr
	RJMP ISR_BOTAO
.org URXCaddr
	RJMP ISR_UART_RX

TABELA:
	.DB 0x40, 0x79, 0x24, 0x30, 0x19, 0x12, 0x02, 0x78
	.DB 0x00, 0x10, 0x08, 0x03, 0x46, 0x21, 0x06, 0x0E
	.DB 0x3F, 0x00
MSG_NOME:
	.DB "Aluno: Pedro Lucas Batista dos Santos Araujo", 13, 10, 0, 0

INICIO:
	LDI R16, LOW(RAMEND)
	OUT SPL, R16
	LDI R16, HIGH(RAMEND)
	OUT SPH, R16
	LDI R16, 0x7F
	OUT DDRB, R16
	OUT PORTB, R16
	SBI DDRD, 1
	SBI PORTD, 2
	LDI R16, (1<<ISC01)
	STS EICRA, R16
	LDI R16, (1<<INT0)
	OUT EIMSK, R16
	LDI R16, HIGH(VALOR_UBRR)
	STS UBRR0H, R16
	LDI R16, LOW(VALOR_UBRR)
	STS UBRR0L, R16
	LDI R16, (1<<RXEN0)|(1<<TXEN0)|(1<<RXCIE0)
	STS UCSR0B, R16
	LDI R16, (1<<UCSZ01)|(1<<UCSZ00)
	STS UCSR0C, R16
	SEI

LOOP:
	RJMP LOOP

ISR_UART_RX:
	PUSH R16
	PUSH R17
	IN R17, SREG
	PUSH R17
	PUSH ZL
	PUSH ZH
	LDS R16, UDR0
	CPI R16, 13
	BREQ FIM_RX
	CPI R16, 10
	BREQ FIM_RX
	RCALL CONVERTE_HEX
	LDI ZH, HIGH(TABELA*2)
	LDI ZL, LOW(TABELA*2)
	ADD ZL, R16
	CLR R17
	ADC ZH, R17
	LPM R16, Z
	OUT PORTB, R16

FIM_RX:
	POP ZH
	POP ZL
	POP R17
	OUT SREG, R17
	POP R17
	POP R16
	RETI

ISR_BOTAO:
	PUSH R16
	PUSH R17
	IN R17, SREG
	PUSH R17
	PUSH ZL
	PUSH ZH
	LDI ZH, HIGH(MSG_NOME*2)
	LDI ZL, LOW(MSG_NOME*2)

ENVIA_LOOP:
	LPM R16, Z+
	CPI R16, 0
	BREQ FIM_BTN
	RCALL ENVIA_BYTE
	RJMP ENVIA_LOOP

FIM_BTN:
	POP ZH
	POP ZL
	POP R17
	OUT SREG, R17
	POP R17
	POP R16
	RETI

CONVERTE_HEX:
	CPI R16, '0'
	BRLO INVALIDO
	CPI R16, '9'+1
	BRLO EH_DIGITO
	CPI R16, 'A'
	BRLO INVALIDO
	CPI R16, 'F'+1
	BRLO EH_MAIUSCULA
	CPI R16, 'a'
	BRLO INVALIDO
	CPI R16, 'f'+1
	BRLO EH_MINUSCULA
	RJMP INVALIDO

EH_DIGITO:
	SUBI R16, '0'
	RET

EH_MAIUSCULA:
	SUBI R16, 'A'-10
	RET

EH_MINUSCULA:
	SUBI R16, 'a'-10
	RET

INVALIDO:
	LDI R16, 16
	RET

ENVIA_BYTE:
	PUSH R17

ESPERA_TX:
	LDS R17, UCSR0A
	SBRS R17, UDRE0
	RJMP ESPERA_TX
	STS UDR0, R16
	POP R17
	RET
